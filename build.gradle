plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.sherter.google-java-format' version '0.9'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.testng:testng:${testngVersion}"
}

test {
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = jacocoVersion
    reportsDirectory = file("$rootProject.projectDir/build/reports/jacoco")
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
    }
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn test

    executionData fileTree(project.rootDir.absolutePath).include("**/build/coverage-reports/*.exec")
    reports {
        xml.required = true
        html.required = true
        csv.required = true
        xml.outputLocation = new File("${buildDir}/reports/jacoco/report.xml")
        html.outputLocation = new File("${buildDir}/reports/jacoco/report.html")
        csv.outputLocation = new File("${buildDir}/reports/jacoco/report.csv")
    }

    onlyIf = {
        true
    }
}

googleJavaFormat {
    toolVersion = "1.2"
    exclude 'src/main/java/module-info.java'
}

jacocoTestReport.dependsOn test
build.dependsOn jacocoTestReport